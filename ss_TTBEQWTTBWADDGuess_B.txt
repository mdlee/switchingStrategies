# switching strategies: TTB, EQWTTB, WADD, Guess

data{
	for (m in 1:nStrategies){
	   baseStrategies[m] = 1
	}
}

model{
  for (i in 1:nSubjects){
	 for (t in 1:nTrials){
	   # Observed decision
	   decision[i, t] ~ dbern(equals(choice[i, t, z[i, stage[i, t]]],   1) * (1 - epsilon[i])
	                     + equals(choice[i, t, z[i, stage[i, t]]],   0) * epsilon[i]
	                     + equals(choice[i, t, z[i, stage[i, t]]], 0.5) * 0.5)
	   # Stage for this subject at this trial
		stage[i, t] = sum(beyond[i, t, 1:max(1, nSwitches[i])]) + 1
		for (k in 1:max(1, nSwitches[i])){
		   # is trial to the right of boundary?
		   beyond[i, t, k] = step(t - tau[i, k] - 1)
	   }
	 }
	 # Model priors
	 epsilon[i] ~ dunif(0, 0.5)
	 z[i, 1] ~ dcat(piPrime[1:nStrategies])
    for (x in 2:(nSwitches[i]+1)){
      z[i, x] ~ dcat(pi[z[i, x-1], 1:nStrategies])
    }
  }
  # Strategy baserate
  piPrime ~ ddirch(baseStrategies[1:nStrategies])
  for (j1 in 1:nStrategies){
    for (j2 in 1:nStrategies){
      basePi[j1, j2] = 1 - equals(j1, j2)  # 0 if j1 == j2, 1 otherwise
    }
    pi[j1, 1:nStrategies] ~ ddirch(basePi[j1, 1:nStrategies])
  }
}