# switching strategies, latent mixture: TTB, Tally, WADD, Guess

data{
	for (m in 1:nStrategies){
	   baseStrategies[m] = 1
	}
}

model{
 for (i in 1:nSubjects){
	for (t in 1:nTrials){
	   # Observed decision
	   decision[i, t] ~ dbern(equals(choice[i, t, z[i, stage[i, t]]], 1) * (1 - epsilon[i])
	                     + equals(choice[i, t, z[i, stage[i, t]]], 0) * epsilon[i]
	                     + equals(choice[i, t, z[i, stage[i, t]]], 0.5) * 0.5)

	   # Stage for this subject at this trial
		stage[i, t] = sum(beyond[i, t, 1:nMaxSwitches]) + 1
		for (k in 1:nMaxSwitches){
		   # is trial to the right of boundary?
		   beyond[i, t, k] = step(t - tau[i, k] - 1)
	   }

	   # Behavior for each strategy on each trial (for the subject, if parameterized)
	   choice[i, t, 1] = TTB(cues[stimA[i, t], ], cues[stimB[i, t], ], searchOrder)
	   outputTALLY[i, t, 1:2] = TALLY(cues[stimA[i, t], ], cues[stimB[i, t], ], searchOrder)
	   choice[i, t, 2] = outputTALLY[i, t, 1]
	   outputWADD[i, t, 1:2] = WADD(cues[stimA[i, t], ], cues[stimB[i, t], ], cueEvidence, searchOrder)
	   choice[i, t, 3] = outputWADD[i, t, 1]
	   choice[i, t, 4] = 0.5
	 }

	 # Model priors
	 epsilon[i] ~ dunif(0, 0.5)
	 # Strategy switch-point priors
	 tau[i, 1:nMaxSwitches] = sort(tauTmp[i, ])
	 for (x in 1:nMaxSwitches){
	  tauTmp[i, x] ~ dunif(1, gamma[i]/(1-gamma[i])*nTrials)
	 }
	 # Strategy use priors
	 z[i, 1] ~ dcat(piPrime[1:nStrategies])
     for (x in 2:(nMaxSwitches+1)){
       z[i, x] ~ dcat(pi[z[i, x-1], 1:nStrategies])
     }
     gamma[i] ~ dnorm(muGamma[x[i]+1], 1/sigmaGamma[x[i]+1]^2)T(0.5, 1)
  }

  # gamma groups
  for (g in 1:2){
  	muGammaTmp[g] ~ dunif(0.5, 1)
  	sigmaGamma[g] ~ dunif(0, 0.5)
  }
  muGamma = sort(muGammaTmp)

  # Strategy baserate
  piPrime ~ ddirch(baseStrategies[1:nStrategies])
  for (j1 in 1:nStrategies){
    for (j2 in 1:nStrategies){
      basePi[j1, j2] = 1 - equals(j1, j2)  # 0 if j1 == j2, 1 otherwise
    }
    pi[j1, 1:nStrategies] ~ ddirch(basePi[j1, 1:nStrategies])
  }
}